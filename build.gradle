plugins {
    id 'java'
	id 'fabric-loom' version '1.7-SNAPSHOT'
	id 'ploceus' version '1.7-SNAPSHOT'
}

repositories {
    maven {
        url 'https://stianloader.org/maven'
    }
    maven {
        url 'https://maven.ornithemc.net/releases'
    }
}

loom {
    clientOnlyMinecraftJar()
    mixin {
        useLegacyMixinAp = false
    }
    accessWidenerPath = file("src/main/resources/launchwrapper.accesswidener")
}

ploceus {
    clientOnlyMappings()
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings ploceus.featherMappings("${project.feather_build}")
	implementation "org.stianloader:micromixin-runtime:0.6.4-a20240825"
	implementation "org.stianloader:micromixin-annotations:0.6.4-a20240825"
}

remapJar {
    targetNamespace = "named"
    // or official if you don't want to remap at runtime and include mappings in build (Currently broken)
    // targetNamespace = "clientOfficial" 
}

jar {
    // Include mappings for runtime remapping
    if(remapJar.targetNamespace.get() == "named") {
        from {
            loom.mappingConfiguration.tinyMappings.toFile()
        }
    }
    rename 'mappings.tiny', 'mappings/mappings.tiny'
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"

    if (JavaVersion.current().isJava9Compatible()) {
    	it.options.release.set(8)
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}